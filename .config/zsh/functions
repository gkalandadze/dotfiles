# Search through zsh history
hgrep() {
  fc -Dlim "*$@*" 1
}

# Restart plasmashell and kwin
restart-kde() {
  plasmashell --replace > /dev/null 2>&1 & disown
  kwin_x11 --replace > /dev/null 2>&1 & disown
}

# Launch youtube video in terminal
lofi() {
  link=${1:-"https://www.youtube.com/watch?v=5qap5aO4i9A"}
  mpv --volume=70 --really-quiet -vo tct --profile=sw-fast --ytdl-format=best "$link"
}

# Coloured manuals
man() {
  env \
    LESS_TERMCAP_mb=$(printf "\e[1;31m") \
    LESS_TERMCAP_md=$(printf "\e[1;31m") \
    LESS_TERMCAP_me=$(printf "\e[0m") \
    LESS_TERMCAP_se=$(printf "\e[0m") \
    LESS_TERMCAP_so=$(printf "\e[1;44;33m") \
    LESS_TERMCAP_ue=$(printf "\e[0m") \
    LESS_TERMCAP_us=$(printf "\e[1;32m") \
    man "$@"
}

# Turn ctrl+z into toggle switch
ctrlz() {
  if [[ $#BUFFER == 0 ]]; then
    fg >/dev/null 2>&1 && zle redisplay
  else
    zle push-input
  fi
}
zle -N ctrlz
bindkey '^Z' ctrlz

# Use ranger to switch directories and bind it to ctrl+o
rangercd () {
  source ranger <$TTY
}
bindkey -s '^o' "rangercd\n"

# Search home directory for files and open when selected
# use ctrl+f for vim and ctrl+s for sublime
fzf_open_vim() {
  find "$HOME" -type f 2>/dev/null | fzf | xargs -r -o vim
}
fzf_open_subl() {
  find "$HOME" -type f 2>/dev/null | fzf | xargs -r -o subl
}
zle -N fzf_open_vim
bindkey '^f' fzf_open_vim
zle -N fzf_open_subl
bindkey '^s' fzf_open_subl

# Exit terminal with ctrl+d
exit_zsh() { exit }
zle -N exit_zsh
bindkey '^D' exit_zsh
